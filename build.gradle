plugins {
    id 'java'
    id 'me.champeau.jmh' version '0.7.2'
    id 'com.palantir.git-version' version '3.1.0'
}

def tagVersion = versionDetails().lastTag
version = tagVersion ? tagVersion.replaceFirst(/^v/, '') : '0.0.0-SNAPSHOT'
group = 'com.github.f1monkey.translit'

println "Building version: $version"

def esVersion = project.hasProperty('esVersion') ? project.esVersion : '8.0.0'
println "Target Elasticsearch version: $esVersion"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.elasticsearch:elasticsearch:8.0.0"

    implementation "org.openjdk.jmh:jmh-core:1.37"
    annotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:1.37"

    testImplementation "org.elasticsearch:elasticsearch:8.19.7"
    testImplementation "org.junit.jupiter:junit-jupiter:5.9.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.2"
    testImplementation "org.junit.platform:junit-platform-launcher"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

test {
    useJUnitPlatform()
}

processResources {
    filesMatching('plugin-descriptor.properties') {
        expand(
            plugin_version: version,
            elasticsearch_version: esVersion
        )
    }
}

jar {
    manifest {
        attributes(
            'Implementation-Title': 'elasticsearch-ru-en-translit-plugin',
            'Implementation-Version': version
        )
    }
}

tasks.register('pluginZip', Zip) {
    archiveBaseName = 'elasticsearch-ru-en-translit-plugin'
    archiveVersion = version
    archiveClassifier = "for-${esVersion}"

    from(jar)
    from("$buildDir/resources/main") {
        include 'plugin-descriptor.properties'
    }
    into('')
}

build.dependsOn pluginZip